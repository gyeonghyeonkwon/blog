plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.asciidoctor.jvm.convert' version '3.3.2' // (1)
}

group = 'com.ll'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    asciidoctorExt // (2)
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework:spring-context-support:6.1.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.assertj:assertj-core:3.19.0"
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'  //(4)
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor' // (3)
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.testcontainers:testcontainers:1.19.0" //redis test
    testImplementation "org.testcontainers:junit-jupiter:1.19.0" // 테스트컨테이너추가
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
}

ext {
    set('snippetsDir', file("build/generated-snippets")) // (5) 테스트성공시
    // 생략
}

test { //(6)
    outputs.dir snippetsDir
    useJUnitPlatform() // 이걸 써야 snippet들이 만들어지는 게 허가됩니다.
}

asciidoctor { //(7)
    inputs.dir snippetsDir //(8)
    configurations 'asciidoctorExt' //(9)
    dependsOn test //(10)

    sources { //(11) index.adoc 이외 다른 adoc 는 html로 생성하지않음
        include("**/index.adoc", "**/common/*.adoc")
    }
    baseDirFollowsSourceFile()
}

// (12) static/docs 폴더 비우기
asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) { //(13)
    dependsOn asciidoctor
    from file("build/docs/asciidoc") //여기에 위치한 html파일을
    into file("src/main/resources/static/docs") //해당 경로로 복사한다.
}

build {
    dependsOn copyDocument
}